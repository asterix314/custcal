if exists (select * from sysobjects where type='P' and name='`存储过程`')
    drop proc `存储过程`
go
/*
select top 10 * from logasset_hs where busintype=`业务摘要`
update logasset_hs set sett_status=0, sett_remark='' where busintype=`业务摘要`

declare @ret as int, @msg as varchar(128)
exec @ret=`存储过程` '1',20160107, 107450, 120100148413, @msg output
select @ret, @msg

select * from stkasset_hs
select * from fundasset_hs
*/
CREATE proc `存储过程`(
 @serverid  char(1)      
,@bizdate   int          
,@sno       int
,@custid    bigint    
,@msg       varchar(128)=null output
)
-- with encryption
as
declare @orgid as varchar(4), @fundid as bigint, @busintype as int, @market as char(1), @stkcode as varchar(10), 
        @stkeffect as numeric(20,2), @matchqty as numeric(20,2), @matchamt as numeric(20,2), @bankcode as varchar(4), 
        @moneytype as char(1), @fundeffect as numeric(20,2), @fee_sxf as numeric(20,2), @businname as varchar(20),
        @fee_jsxf as numeric(20,2), @fee_ghf as numeric(20,2), @fee_yhs as numeric(20,2), @qtfee as numeric(20,2), 
        @ordersno as bigint, @orderdate as int, @sett_status as char(1), @rowcount as int, @ret as int
declare @fundzqjy_ch as numeric(20,2), @qtsz_ch as numeric(20,2), @fundadjust_ch as numeric(20,2),
        @fundother_ch as numeric(20,2)
        
 select @orgid=orgid, @fundid=fundid, @busintype=busintype, @market=market, @stkcode=stkcode, 
        @stkeffect=stkeffect, @matchqty=matchqty, @fundeffect=fundeffect, @matchamt=matchamt,
        @businname=case when charindex('息',remark)>0 or charindex('费',remark)>0 or charindex('税',remark)>0 or charindex('佣金',remark)>0
                        then '其它收支'
                        when charindex('异常',remark)>0 or charindex('差错',remark)>0
                        then '内部调整'
                        when charindex('继承',remark)>0 or charindex('休眠',remark)>0 or charindex('垫资',remark)>0 or remark like '%企%债%'
                        then '外部调整'
			else '其它调整' end,
        @fee_sxf=fee_sxf, @fee_jsxf=fee_jsxf, @fee_ghf=fee_ghf, @fee_yhs=fee_yhs,
        @qtfee=fee_qsf+fee_jygf+fee_jsf+fee_zgf+fee_qtf+feefront,
        @bankcode=bankcode, @moneytype=moneytype, @ordersno=ordersno, @orderdate=orderdate,
        @sett_status=sett_status
   from logasset_hs with (nolock, index=logasset_hs_pk)
  where sno=@sno and bizdate=@bizdate and serverid=@serverid and custid=@custid and
        busintype=`业务摘要` -- `业务名称`

begin try
    if (@sett_status is null)
       begin
          select @msg='该笔流水不存在或调用错误.'
          raiserror(' %s', 12, 1, @msg) with SETERROR
       end

    if (@sett_status='3')
       begin
          select @msg='该笔流水已处理.'
          return 0
       end

    if (@fundeffect=0)
       begin
          select @msg='资金发生与该业务不符.'
          raiserror(' %s', 12, 1, @msg) with SETERROR
       end

    if (@stkeffect!=0)
       begin
          select @msg='股份发生与该业务不符.'
          raiserror(' %s', 12, 1, @msg) with SETERROR
       end

    if (@fee_sxf!=0 or @fee_jsxf!=0 or @fee_ghf!=0 or @fee_yhs!=0 or @qtfee!=0)
       begin
          select @msg='费用金额异常.'
          raiserror(' %s', 12, 1, @msg) with SETERROR
       end

begin tran
   select  @fundzqjy_ch=case when @businname in ('其它收支','内部调整') then @fundeffect else 0 end,
           @qtsz_ch=case when @businname='其它收支' then @fundeffect else 0 end,
           @fundadjust_ch=case when @businname='外部调整' then @fundeffect else 0 end,
           @fundother_ch=case when @businname='其它调整' then @fundeffect else 0 end
           
    update fundasset_hs
       set fundbal=fundbal+@fundeffect, fundbal_ch=fundbal_ch+@fundeffect,
           fundzqjy_ch=fundzqjy_ch+@fundzqjy_ch,
           qtsz=qtsz+@qtsz_ch, qtsz_ch=qtsz_ch+@qtsz_ch,
           fundadjust=fundadjust+@fundadjust_ch, fundadjust_ch=fundadjust_ch+@fundadjust_ch,
           fundother=fundother+@fundother_ch, fundother_ch=fundother_ch+@fundother_ch
     where fundid=@fundid and custid=@custid and orgid=@orgid and serverid=@serverid and
           moneytype=@moneytype   
          
    select @rowcount=@@ROWCOUNT
    if (@rowcount>1)
       begin
          select @msg='更新资金记录条数错误(rowcount='+CONVERT(varchar, @rowcount)+').'
          raiserror(' %s', 12, 1, @msg) with SETERROR
       end
    else if (@rowcount=0)
       begin
          insert fundasset_hs
                 (serverid, orgid, custid, fundid, moneytype, 
                 fundlastbal, fundbal, fundbal_ch, 
                 fundsave, fundsave_ch, fundunsave, fundunsave_ch, 
                 fundzqjy_ch, fundzqjy0_ch, fundbxyw, fundbxyw_ch, 
                 fundotc, fundotc_ch, fundkfjj, fundkfjj_ch, 
                 fundgold, fundgold_ch, fundother, fundother_ch, 
                 fundloan, fundloan_ch, funddebt, funddebt_ch, 
                 funduncome, funduncome_ch, fundunpay, fundunpay_ch, 
                 fundadjust, fundadjust_ch, 
                 fundintr, fundintr_ch, fundaward, fundaward_ch, 
                 rzlx, rzlx_ch, rcsr, rcsr_ch, 
                 lxs, lxs_ch, qtsz, qtsz_ch, 
                 bankcode, tjdate, remark)
          select @serverid, @orgid, @custid, @fundid, @moneytype, 
                 fundlastbal=0, fundbal=@fundeffect, fundbal_ch=@fundeffect, 
                 fundsave=0, fundsave_ch=0, fundunsave=0, fundunsave_ch=0, 
                 fundzqjy_ch=@fundzqjy_ch, fundzqjy0_ch=0, fundbxyw=0, fundbxyw_ch=0, 
                 fundotc=0, fundotc_ch=0, fundkfjj=0, fundkfjj_ch=0, 
                 fundgold=0, fundgold_ch=0, fundother=@fundother_ch, fundother_ch=@fundother_ch, 
                 fundloan=0, fundloan_ch=0, funddebt=0, funddebt_ch=0, 
                 funduncome=0, funduncome_ch=0, fundunpay=0, fundunpay_ch=0, 
                 fundadjust=@fundadjust_ch, fundadjust_ch=@fundadjust_ch, 
                 fundintr=0, fundintr_ch=0, fundaward=0, fundaward_ch=0, 
                 rzlx=0, rzlx_ch=0, rcsr=0, rcsr_ch=0, 
                 lxs=0, lxs_ch=0, qtsz=@qtsz_ch, qtsz_ch=@qtsz_ch, 
                 bankcode=@bankcode, tjdate=0, remark=''
           end

      select @msg='`业务名称`核算处理成功'
      update logasset_hs
         set sett_status=3, sett_remark=@msg
       where sno=@sno and bizdate=@bizdate and serverid=@serverid
   
   commit tran
   return 0
end try

begin catch
   if @@trancount>0
       rollback tran

   select @msg='`业务名称`失败:'+error_message()+''
   
   update logasset_hs 
      set sett_status=4, sett_remark=@msg
    where sno=@sno and bizdate=@bizdate and serverid=@serverid

   return -1
end catch
go
